import groovy.json.JsonSlurper

pluginManagement {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
        maven {
            name = "Fabric"
            url = "https://maven.fabricmc.net/"
        }
        maven {
            name = "Jitpack"
            url = "https://jitpack.io"
        }
    }

    resolutionStrategy {
        eachPlugin {
            if (requested.id.id == "com.replaymod.preprocess") {
                useModule("com.github.Fallen-Breath:preprocessor:${requested.version}")
            }
        }
    }
}

def settings = new JsonSlurper().parseText(file("settings.json").text)

for (String version : settings.versions) {
    include(":$version")
    ProjectDescriptor proj = project(":$version")
    proj.setProjectDir(file("versions/$version"))
    proj.setBuildFileName("../${settings.versions.get(0)}/build.gradle")
}

include(":fabricWrapper")
