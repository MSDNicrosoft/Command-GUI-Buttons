plugins {
    id("fabric-loom").version("1.3-SNAPSHOT").apply(false)
    id("com.replaymod.preprocess").version("SNAPSHOT")
    id("org.ajoberstar.grgit").version("5.2.1")
    id("net.kyori.blossom").version("1.3.1").apply(false)
}

preprocess {
    def mc1165 = createNode("1.16.5", 1_16_05, "mojang")
    def mc1171 = createNode("1.17.1", 1_17_01, "mojang")
    def mc1182 = createNode("1.18.2", 1_18_02, "mojang")
    def mc1192 = createNode("1.19.2", 1_19_02, "mojang")
    def mc1193 = createNode("1.19.3", 1_19_03, "mojang")
    def mc1194 = createNode("1.19.4", 1_19_04, "mojang")
    def mc1201 = createNode("1.20.1", 1_20_01, "mojang")

    mc1165.link(mc1171, null)
    mc1171.link(mc1182, null)
    mc1182.link(mc1192, null)
    mc1192.link(mc1193, null)
    mc1193.link(mc1194, null)
    mc1201.link(mc1194, null)
}

def ENV = System.getenv()
String realVersion = "${project.mod_version}+build."
realVersion += ENV.GITHUB_RUN_NUMBER ? ENV.GITHUB_RUN_NUMBER : new Date().format("yyyyMMdd.HHmmss")
if (grgit != null) {
    realVersion += grgit.status().isClean() ? "+${grgit.head().abbreviatedId}" : "+uncommited"
} else {
    realVersion += "+nogit"
}

setVersion(realVersion)

tasks.register('cleanPreprocessSources') {
    doFirst {
        subprojects {
            def path = project.projectDir.toPath().resolve('build/preprocessed')
            path.toFile().deleteDir()
        }
    }
}
