plugins {
    id("fabric-loom").version("1.11.5").apply(false)
    id("com.replaymod.preprocess").version("0a04a56")
    id("org.ajoberstar.grgit").version("5.3.2")
    id("top.hendrixshen.replace-token").version("1.1.3").apply(false)
}

preprocess {
    strictExtraMappings = false

    def mc1165 = createNode("1.16.5", 1_16_05, "mojang")
    def mc1171 = createNode("1.17.1", 1_17_01, "mojang")
    def mc1182 = createNode("1.18.2", 1_18_02, "mojang")
    def mc1192 = createNode("1.19.2", 1_19_02, "mojang")
    def mc1193 = createNode("1.19.3", 1_19_03, "mojang")
    def mc1194 = createNode("1.19.4", 1_19_04, "mojang")
    def mc1206 = createNode("1.20.6", 1_20_06, "mojang")
    def mc1215 = createNode("1.21.5", 1_21_05, "mojang")
    def mc1217 = createNode("1.21.7", 1_21_07, "mojang")

    mc1165.link(mc1171, null)
    mc1171.link(mc1182, null)
    mc1182.link(mc1192, null)
    mc1192.link(mc1193, null)
    mc1193.link(mc1194, null)
    mc1194.link(mc1206, null)
    mc1206.link(mc1215, null)
    mc1215.link(mc1217, null)
}

def ENV = System.getenv()
String realVersion = "${project.mod_version}+build."
realVersion += ENV.GITHUB_RUN_NUMBER ? ENV.GITHUB_RUN_NUMBER : new Date().format("yyyyMMdd.HHmmss")
if (grgit != null) {
    realVersion += grgit.status().isClean() ? "+${grgit.head().abbreviatedId}" : "+uncommited"
} else {
    realVersion += "+nogit"
}

version = realVersion

tasks.register("cleanPreprocessSources") {
    doFirst {
        subprojects {
            layout.buildDirectory.dir("preprocessed").getOrNull().asFile.deleteDir()
        }
    }
}
