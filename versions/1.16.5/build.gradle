plugins {
    alias(libs.plugins.fabric.loom)
    alias(libs.plugins.preprocessor)
    alias(libs.plugins.yamlang)
    alias(libs.plugins.replace.token)
}

repositories {
    maven {
        name = "CottonMC"
        url = "https://server.bbkr.space/artifactory/libs-release"
    }

    maven {
        name = "Modrinth Maven"
        url = "https://api.modrinth.com/maven"
    }

    maven {
        name = "HangBing Proxy"
        url = "https://repository.hanbings.io/proxy"
    }

    maven {
        name = "Nyan Maven"
        url = "https://maven.hendrixshen.top/releases"
    }

    mavenCentral()
    mavenLocal()
}

int mcVersion = 1

preprocess {
    mcVersion = vars.get().get("MC")
}

dependencies {
    // Development environment
    minecraft("com.mojang:minecraft:${project.minecraft_version}")
    mappings(loom.officialMojangMappings())
    modApi(libs.fabric.loader)

    // Annotation processor
    modApi(libs.lombok)
    annotationProcessor(libs.lombok)

    // Dependencies
    modApi("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}")
    modApi("io.github.cottonmc:LibGui:${project.libgui_version}")
    modApi("maven.modrinth:modmenu:${project.modmenu_version}")

    // Make development client more comfortable.
    modRuntimeOnly("top.hendrixshen.magiclib:magiclib-better-dev-${project.minecraft_version}${project.minecraft_version.size() == 4 ? ".0" : ""}-fabric:0.3.68-beta") {
        transitive = false
    }

    modRuntimeOnly(libs.magiclib.core.fabric) {
        transitive = false
    }

    runtimeOnly(project(path: ":fabricWrapper"))
}

tasks.named("runClient") {
    doFirst {
        layout.projectDirectory.file("run/client/options.txt").asFile.with { File file ->
            if (!file.exists()) {
                file.parentFile.mkdirs()
                file.withWriter { BufferedWriter writer ->
                    writer.writeLine("autoJump:false")
                    writer.writeLine("enableVsync:false")
                    writer.writeLine("forceUnicodeFont:true")
                    writer.writeLine("fov:1.0")
                    writer.writeLine("gamma:1.0")
                    writer.writeLine("guiScale:3")
                    writer.writeLine("lang:${Locale.getDefault().toString()}")
                    writer.writeLine("maxFps:260")
                    writer.writeLine("renderDistance:10")
                    writer.writeLine("soundCategory_master:0.0")
                }
            }
            null // We don't need to return anything.
        }
    }
}

// Useless, but we write here.
// Setup server default settings.
tasks.named("runServer") {
    doFirst {
        // Agree eula before server init.
        layout.projectDirectory.file("run/server/eula.txt").asFile.with { File file ->
            if (!file.exists()) {
                file.parentFile.mkdirs()
                file.withWriter { BufferedWriter writer ->
                    writer.writeLine("eula=true")
                }
            }
            null // We don't need to return anything.
        }
    }
}

loom {
    runConfigs.configureEach {
        // Dump modified classes automatically.
        property("mixin.debug.export", "true")
    }

    // We need large window.
    runConfigs.client {
        programArgs([
                "--width",
                "1920",
                "--height",
                "1080",
        ])
        runDir("run/client")
    }

    // Useless, but we write here.
    runConfigs.server {
        runDir("run/server")
    }
}

base {
    version = project.parent.version
    group = project.maven_group
    archivesName = "${project.archives_base_name}-mc${project.minecraft_out}"
}

replaceToken {
    targetSourceSets.set([sourceSets.main])

    replace("@MOD_ID@", project.mod_id)
    replace("@MINECRAFT_VERSION_ID@", project.minecraft_version.replace(".", "_"))
    replaceIn("work/msdnicrosoft/commandbuttons/CommandButtonsReference")
}

yamlang {
    targetSourceSets = [sourceSets.main]
    inputDir = 'assets/mgbuttons/lang'
}

processResources {
    outputs.upToDateWhen { false }

    inputs.property("minecraft_dependency", project.minecraft_dependency)
    inputs.property("minecraft_version_id", project.minecraft_version.replace(".", "_"))
    inputs.property("minecraft_version_out", project.minecraft_out)
    inputs.property("mod_id", project.mod_id)
    inputs.property("mod_name", project.mod_name)
    inputs.property("mod_license", project.mod_license)
    inputs.property("mod_sources", project.mod_sources)
    inputs.property("mod_version", project.version)

    filesMatching("fabric.mod.json") {
        expand([
                "minecraft_dependency" : inputs.properties["minecraft_dependency"],
                "minecraft_version_id" : inputs.properties["minecraft_version_id"],
                "minecraft_version_out": inputs.properties["minecraft_version_out"],
                "mod_id"               : inputs.properties["mod_id"],
                "mod_name"             : inputs.properties["mod_name"],
                "mod_license"          : inputs.properties["mod_license"],
                "mod_sources"          : inputs.properties["mod_sources"],
                "mod_version"          : inputs.properties["mod_version"],
        ])
    }
}

java {
    if (mcVersion > 12004) {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    } else if (mcVersion > 11701) {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    } else if (mcVersion > 11605) {
        sourceCompatibility = JavaVersion.VERSION_16
        targetCompatibility = JavaVersion.VERSION_16
    } else {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    withSourcesJar()
}

jar {
    from(project.rootProject.file("LICENSE"))
}

tasks.register("cleanRun") {
    doLast {
        // Clean up the run directory.
        layout.projectDirectory.dir("run").asFile.deleteDir()
    }
}
